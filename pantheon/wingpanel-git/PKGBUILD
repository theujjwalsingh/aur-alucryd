# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-git
pkgver=r382.dd64ba8
pkgrel=1
pkgdesc='Stylish top panel that holds indicators and spawns an application launcher'
arch=('x86_64')
url='https://github.com/elementary/wingpanel'
license=('GPL3')
groups=('pantheon-unstable')
depends=('glib2' 'glibc' 'gtk3' 'libgee' 'mutter'
         'libgala.so' 'libgranite.so')
makedepends=('cmake' 'gala-git' 'git' 'granite-git' 'vala')
provides=('wingpanel' 'libwingpanel-2.0.so')
conflicts=('wingpanel')
replaces=('wingpanel-bzr')
source=('git+https://github.com/elementary/wingpanel.git')
sha256sums=('SKIP')

pkgver() {
  cd wingpanel

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  arch-meson wingpanel build \
    -Dlibunity='false'
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
